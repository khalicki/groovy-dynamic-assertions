plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.khalicki'
version = '0.2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.9'

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'junit:junit:4.13.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
    dependsOn(groovydoc)
    archiveClassifier = "javadoc"
    from groovydoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/khalicki/groovy-dynamic-assertions")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        localSnapshot(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact groovydocJar
        }
        github(MavenPublication) {
            from(components.java)
        }
    }
}